Script         = Context { PatternDef } .
Context        = CONTEXT ADLid [IN ( DUTCH | ENGLISH )] \n 
                  [ ":" Expr [ Binding ] ] \n
                  { ContextElement }  \n
				  ENDCONTEXT .
Binding        = BINDING BIND Declaration TOPHP (Conid | String)
                   { "," BIND Declaration TOPHP (Conid | String) } .

ContextElement = ( PatElem
                 | UsePattern
                 | Service
				 | SQLplug
				 | Phpplug
				 | RoleService
				 | RoleRelation
				 | Population
				 ).
UsePattern     = USE PATTERN ADLid .
PatternDef     = PATTERN ADLid [IN ( DUTCH | ENGLISH )] \n
                 { PatElem } ENDPATTERN .
PatElem        = ( RuleDef
                 | Gen
				 | Declaration
				 | ConceptDef
				 | Keydef
				 | Explain
				 ).
Explain        = PURPOSE ExplObj [IN ( DUTCH | ENGLISH )] [REF String] Expl .
ExplObj        = ( CONCEPT (Conid | String)
                 | RELATION Morphism 
			     | RULE ADLid
			     | KEY ADLid
			     | SERVICE ADLid
     		     | PATTERN ADLid
			     | CONTEXT ADLid
			     ).
Population     = POPULATION Morphism CONTAINS Content .
RuleDef        = (  RULE ADLid   \n
                   ( Expr
				   | Expr "|-" Expr
				   | Expr "-|" Expr
				   | Expr "="  Expr
                   )  \n
				   { MEANING [IN ( DUTCH | ENGLISH )] Expl }
                 ).
Gen            = GEN (Conid | String) ISA (Conid | String) .
preOp          =  "-" .				 
postOp         = { ( "~" | "+" | "*") } .
Term1          = ( PreTerm { ";" PreTerm } 
                 | PreTerm { "!" PreTerm }
				 ) .
Term2          = ( Term1 "\" Term1 
                 | Term1 "/" Term1 
				 ).
Term3          = ( Term2 { "\/" Term2 }
                 | Term2 { "/\" Term2 }
				 | Term2  "-"  Term2 
				 ) .
Expr           = ( Term3 "=" Term3 
                 | Term3 "|-" Term3 
                 | Term3 "-|" Term3 
				 ).
PreTerm        = PostTerm | "-" PreTerm .
PostTerm       = PostTerm ( "~" | "*" | "+" ) | Term .
Term           = Morphism | "(" Expr ")" .
Morphism       = ( ( I | Atom ) 
                      [ "[" Concept "]" ] 
                 | ( V | Varid ) 
				      [ "[" ( Concept 
				            | Concept "*" Concept 
					        ) "]" ] 
				 ) . 
Atom           = "'" "a..z,A..Z,0..9,_-etc" "'" . 
Concept        = ( SELF | Conid | String ) .
LabelProps     = ADLid [ "{" String { "," String } "}" ] ":" .
ConceptDef     = CONCEPT (Conid | String) [BYPLUG] String [REF String] .				 
KeyDef         = KEY LabelProps Concept "("	KeyAtt {"," KeyAtt} ")".
KeyAtt         = [ LabelProps ] Expr.
RoleService    = ROLE ADLid { "," ADLid } USES ADLid { "," ADLid } .
RoleRelation   = ROLE ADLid { "," ADLid } EDITS Morphism { "," Morphism } .
Sqlplug        = SQLPLUG Obj . 
Phpplug        = PHPPLUG Obj . 
Service        = SERVICE ADLid \n
                  [ "(" Morphism { "," Morphism } ")" ] \n
                  [ "{" ADLid { ADLid } [ "," ADLid { ADLid } ] "}" ] \n
				  ":" Expr \n
				  [ "=" "[" [ Obj { "," Obj } ] "]" ].
Obj            = LabelProps Expr \n
                 [ ALWAYS  {( UNI  | TOT | PROP )} ]  \n
                 [ "=" "[" Obj { "," Obj } "]" ].		
Declaration    = Varid "::" Concept ( "*" | "->" ) Concept \n
 				 [Props] [Pragma] [BYPLUG] "." .	
Content        = "[" [ Record { ";" Record } ] "]" .
Record         = "(" String "," String ")". 
Props          = "[" [ Prop { "," Prop } ] "]" .
Prop           = ( UNI | INJ |  SUR| TOT | SYM | ASY | TRN | RFX ). 
Pragma         = PRAGMA String { String } . 	

