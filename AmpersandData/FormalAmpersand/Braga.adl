CONTEXT AST IN ENGLISH LATEX
-- The comments for this script can be found in Atoms.doc
INCLUDE "Atoms.adl"
--INCLUDE "Rules.adl"
INCLUDE "Braga.ifc"
INCLUDE "Braga.xlsx"

PROCESS Terms
CONCEPT SESSION ""
RELATION src[Signature*Concept]
RELATION tgt[Signature*Concept]
RELATION type[Relation*Signature]
RELATION pretype[TypeTerm*Concept]
RELATION sub[TypeTerm*TypeTerm]
RELATION typehalf[Term*Signature]
RELATION dom[Term*TypeTerm] [UNI]    -- TOT is computed by the exec-engine
RELATION cod[Term*TypeTerm] [UNI]    -- TOT is computed by the exec-engine
RELATION popset[Concept*TypeTerm] [UNI] -- TOT is computed by the exec-engine
RELATION id[Concept*Term] [UNI] -- TOT is computed by the exec-engine
RELATION vee[Signature*Term] [UNI] -- TOT is computed by the exec-engine
RELATION sub[TypeTerm*TypeTerm]
MEANING "s sub g  means that each element of type-term  s  is defined to be an element of type-term  g  as well."
RELATION subStar[TypeTerm*TypeTerm]
MEANING "s subStar g  means that each element of type-term  s  is defined or inferred to be an element of type-term  g."
RELATION repr[Term*Representation] [UNI]
RELATION lhs[Term*Term]
RELATION rhs[Term*Term]
RELATION operator[Term*Operator]
RELATION lhs[Rule*Term]
RELATION rhs[Rule*Term]
REPRESENT Representation TYPE ALPHANUMERIC
REPRESENT Identifier TYPE ALPHANUMERIC
REPRESENT TypeTerm TYPE ALPHANUMERIC

--Step: add type rules for source and target.
ROLE ExecEngine MAINTAINS "dom is total"
RULE "dom is total" : I |- dom;dom~
VIOLATION (TXT "{EX}_; InsPair_;dom_;Term_;", SRC I, TXT "_;TypeTerm_;dom(", SRC repr, TXT ")")

ROLE ExecEngine MAINTAINS "cod is total"
RULE "cod is total" : I |- cod;cod~
VIOLATION (TXT "{EX}_; InsPair_;cod_;Term_;", SRC I, TXT "_;TypeTerm_;cod(", SRC repr, TXT ")")

ROLE ExecEngine MAINTAINS typeTgt
RULE typeTgt : type~;target |- tgt
VIOLATION (TXT "{EX} InsPair;tgt;Signature;", SRC I, TXT ";Concept;", TGT I)
ROLE ExecEngine MAINTAINS typeSrc
RULE typeSrc : type~;source |- src
VIOLATION (TXT "{EX} InsPair;src;Signature;", SRC I, TXT ";Concept;", TGT I)

ROLE ExecEngine MAINTAINS totSign
RULE totSign : I[Relation] |- type;type~
VIOLATION (TXT "{EX} NewStruct;Signature"
          ,TXT ";type;Relation;", SRC I, TXT ";Signature;_NEW"
          )

ROLE ExecEngine MAINTAINS "dom pretype id"
RULE "dom pretype id" : id;dom |- pretype~
VIOLATION (TXT "{EX}_; InsPair_;pretype_;TypeTerm_;", TGT I, TXT "_;Concept_;", SRC I)

ROLE ExecEngine MAINTAINS "cod pretype id"
RULE "cod pretype id" : id;cod |- pretype~
VIOLATION (TXT "{EX}_; InsPair_;pretype_;TypeTerm_;", TGT I, TXT "_;Concept_;", SRC I)

ROLE ExecEngine MAINTAINS "popset is total"
RULE "popset is total" : I[Concept] |- popset;popset~
VIOLATION (TXT "{EX}_; InsPair_;popset_;Concept_;", SRC I, TXT "_;TypeTerm_;pop(", SRC name, TXT ")")

ROLE ExecEngine MAINTAINS "id is total"
RULE "id is total" : I[Concept] |- id;id~
VIOLATION (TXT "{EX}_; InsPair_;id_;Concept_;", SRC I, TXT "_;Term_;id(", SRC I, TXT ")")

ROLE ExecEngine MAINTAINS "vee is total"
RULE "vee is total" : I[Signature] |- vee;vee~
VIOLATION (TXT "{EX}_; InsPair_;vee_;Signature_;", SRC I, TXT "_;Term_;vee(", SRC src, TXT ",", SRC tgt, TXT ")")

ROLE ExecEngine MAINTAINS "Insert into pretype"
RULE "Insert into pretype" : subStar;popset~ |- pretype
VIOLATION (TXT "{EX}_; InsPair_;pretype_;TypeTerm_;", SRC I, TXT "_;Concept_;", TGT I)

ROLE ExecEngine MAINTAINS "dom pretype vee"
RULE "dom pretype vee" : src~;vee;dom |- pretype~
VIOLATION (TXT "{EX}_; InsPair_;pretype_;TypeTerm_;", TGT I, TXT "_;Concept_;", SRC I)

ROLE ExecEngine MAINTAINS "cod pretype vee"
RULE "cod pretype vee" : tgt~;vee;cod |- pretype~
VIOLATION (TXT "{EX}_; InsPair_;pretype_;TypeTerm_;", TGT I, TXT "_;Concept_;", SRC I)

ROLE ExecEngine MAINTAINS subsIns
RULE subsIns : sub;subStar\/subStar;sub\/I |- subStar
VIOLATION (TXT "{EX}_; InsPair_;subStar_;TypeTerm_;", SRC I, TXT "_;TypeTerm_;", TGT I)
ROLE ExecEngine MAINTAINS subsDel
RULE subsDel : subStar |- sub;subStar\/subStar;sub\/I
VIOLATION (TXT "{EX}_; DelPair_;subStar_;TypeTerm_;", SRC I, TXT "_;TypeTerm_;", TGT I)

ROLE ExecEngine MAINTAINS "INS isa INTO sub"
RULE "INS isa INTO sub" : popset~;isa;popset |- sub
VIOLATION (TXT "{EX}_; InsPair_;sub_;TypeTerm_;", SRC I, TXT "_;TypeTerm_;", TGT I)

ROLE User MAINTAINS "Unambiguous pretype"
RULE "Unambiguous pretype" : pretype~;pretype |- I[Concept] - isas;isas~
MEANING "For every type-term, there is at most one concept that is the pretype of that type-term."
ROLE User MAINTAINS "Complete pretype"
RULE "Complete pretype" : I[TypeTerm] |- pretype;pretype~
MEANING "Every type-term must have a concept as its pretype."

ROLE ExecEngine MAINTAINS "Every I-term has a representation"
RULE "Every I-term has a representation" : id |- id;repr;repr~
VIOLATION (TXT "{EX}_; InsPair_;repr_;Term_;", SRC id, TXT "_;Representation_;I[", SRC name, TXT "]")

ROLE ExecEngine MAINTAINS "Every V-term has a representation"
RULE "Every V-term has a representation" : vee |- vee;repr;repr~
VIOLATION (TXT "{EX}_; InsPair_;repr_;Term_;", SRC vee, TXT "_;Representation_;V[", SRC src;name, TXT "*", SRC tgt;name, TXT "]")

ENDPROCESS

ENDCONTEXT