CONTEXT AST IN ENGLISH LATEX
--INCLUDE "Atoms.docadl" TODO: Fix documentation, so it compiles again and hence can be included here

PROCESS Atoms
RELATION repr[Atom*TEXT] [UNI,TOT]
MEANING "Every atom has a representation." -- Identifiability is not a property of this relation, and therefore should be specified by a rule.
REPRESENT TEXT TYPE ALPHANUMERIC

RELATION pop[Atom*Concept]
MEANING "If a pop c, we say that atom  a  is in the population of concept  c."

RELATION name[Concept*Name] [UNI,TOT]
MEANING "Every concept has a name" -- Identifiability is not a property of this relation, and therefore should be specified by a rule.
REPRESENT Name TYPE ALPHANUMERIC

RULE "Identifiability of concepts": name;name~ |- I[Concept]
MEANING "The name of a concept identifies that concept."
VIOLATION (TXT "The name \"", SRC I, TXT "\" is already in use")

RULE "Identifiability of atoms": repr;repr~ /\ pop;pop~ |- I[Atom]
MEANING "An Atom is identifiable by its representation and its concept."
VIOLATION (TXT "The name \"", SRC I, TXT "\" is already in use")

RELATION isa[Concept*Concept] [IRF,ASY]
MEANING "s isa g  means that each element of concept  s  is defined to be an element of concept  g  as well."
RELATION isas[Concept*Concept]
MEANING "the transitive closure isa+ ."
RELATION isaCopy[Concept*Concept] -- necessary only for calling the Transitive closure function.
ROLE ExecEngine MAINTAINS "Compute transitive closure of isa"
RULE "Compute transitive closure of isa" : isa = isaCopy
VIOLATION (TXT "{EX} TransitiveClosure;isa;Concept;isaCopy;isas")

RELATION name[Relation*Name] [UNI,TOT] 
RELATION source[Relation*Concept] [UNI,TOT] 
RELATION target[Relation*Concept] [UNI,TOT] 

RELATION in[Pair*Relation] [UNI,TOT]
RELATION l[Pair*Atom] [UNI,TOT]
RELATION r[Pair*Atom] [UNI,TOT]

ROLE ExecEngine MAINTAINS insPop, delPop
RULE insPop : l~;in;source;(isas\/I) \/ r~;in;target;(isas\/I) |- pop
VIOLATION (TXT "{EX} InsPair;pop;Atom;", SRC I, TXT ";Concept;", TGT I)
RULE delPop : pop |- l~;in;source;(isas\/I) \/ r~;in;target;(isas\/I)
VIOLATION (TXT "{EX} DelPair;pop;Atom;", SRC I, TXT ";Concept;", TGT I)

ENDPROCESS
ENDCONTEXT