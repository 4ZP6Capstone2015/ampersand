CONTEXT AST IN ENGLISH 
INCLUDE "Terms.adl"
INCLUDE "Braga.xlsx"

INTERFACE Overview : '_SESSION'
TABS[ concepts :V[SESSION*Concept]
      BOX<SCOLS>
           [ name         : I[Concept]
           , "isa"        : isa
           , "isa+"       : isaPlus
           , "isa~"       : isa~
           , "isa+~"      : isaPlus~
           , atoms        : pop~
           , "type-term"  : popset
           , term         : id~;repr
           ]
    , signatures : V[SESSION*Signature]
      BOX<SCOLS>
           [ signature  : I[Signature]
           , src   : src
           , tgt   : tgt
           ]
    , terms : V[SESSION*Term]
      BOX<SCOLS>
           [ ""         : repr
           ]
    , "pretypes" : V[SESSION*Concept]
      BOX<SCOLS>
           [ Concept      : I
           , "type-terms" : pretype~
           , "star-terms" : pretype~;subStar~
           , "dom-terms"  : pretype~;subStar~;dom~
           , "cod-terms"  : pretype~;subStar~;cod~
           ]
    , "dom-terms" : V[SESSION*TypeTerm];dom~
      BOX<SCOLS>
           [ dom  : dom
           , term : repr
           ]
    , "cod-terms" : V[SESSION*TypeTerm];cod~
      BOX<SCOLS>
           [ cod  : cod
           , term : repr
           ]
    , "type-terms" : V[SESSION*TypeTerm]
      BOX<SCOLS>
           [ "type-term" : I
           , pretype : pretype
           , "sub"   : sub
           , "sub+"  : subPlus
           , "sub*"  : subStar
           , "sub~"  : sub~
           , "sub+~" : subPlus~
           , "sub*~" : subStar~
           ]
    , "id-terms" : V[SESSION*Concept];id~
      BOX<SCOLS>
           [ id   : id
           ]
    , atoms : V[SESSION*Atom]
      BOX<SCOLS>
           [ repr : I
           , type : pop - pop;isaPlus
           , pop  : pop
           ]
    , pairs : V[SESSION*Pair]
      BOX<SCOLS>
           [ pair           : I[Pair]
           , "relation(s)"  : in
           ]
    , relations : V[SESSION*Relation]
      BOX<SCOLS>
           [ relation   : I[Relation]
           , "pair(s)"  : in~
           , sources    : sources
           , targets    : targets
           ]
    , garbage : V[SESSION*Atom];(I - pop;pop~)
      COLS [ repr : repr
           ]
    ]

VIEW Atom : Atom(repr)  -- When observing an atom, its representation is what you can see.

INTERFACE Atom(repr[Atom*Representation]) FOR Ampersand : I[Atom]
ROWS [ repr : repr
     ]

VIEW Concept : Concept(name)

INTERFACE Concept(name[Concept*Identifier],isa) FOR Ampersand : I[Concept]
ROWS [ name             : name
     , generalizations  : isa
     , specializations  : isa~
     ]

VIEW Pair : Pair(TXT "(",l;repr,TXT ",",r;repr,TXT ")")

INTERFACE Pair(l,r,in[Pair*Relation]) FOR Ampersand: I[Pair]
ROWS [ pair           : I
     , l              : l
     , r              : r
     , "relation(s)"  : in
     ]

VIEW Signature : Signature(TXT "[", src;name,TXT "*", tgt;name,TXT "]")

INTERFACE Signature(signature, src, tgt) FOR Ampersand: I[Signature]
ROWS [ signature : I
     , src  : src
     , tgt  : tgt
     ]

VIEW Relation : Relation(name, TXT "[", source;name, TXT "*", target;name, TXT "]")

INTERFACE Relation(name[Relation*Identifier], source, target) FOR Ampersand: I[Relation]
ROWS [ name    : name
     , source  : source
     , target  : target
     ]

ENDCONTEXT