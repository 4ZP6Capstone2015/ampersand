CONTEXT AST IN ENGLISH LATEX
-- The comments for this script can be found in Atoms.doc
INCLUDE "Atoms.adl"
INCLUDE "DomainAnalysis.adl"

PROCESS "Declaring Relations"
    RELATION id[Term*Concept] [UNI,INJ]          -- SUR is maintained in RULE "id is surjective"
    RELATION src[Signature*Concept] [UNI]        -- TOT is maintained in RULE signatureSrc
    RELATION tgt[Signature*Concept] [UNI]        -- TOT is maintained in RULE signatureTgt
    RELATION signature[Relation*Signature] [UNI] -- TOT is maintained in RULE totSign

    ROLE ExecEngine MAINTAINS totSign
    RULE totSign : I[Relation] |- signature;signature~
    VIOLATION (TXT "{EX} NewStruct;Signature" -- maak een nieuw atoom in concept Signature
                    ,TXT ";signature;Relation;", SRC I, TXT ";Signature;_NEW"
              )
    ROLE ExecEngine MAINTAINS signatureSrcIns
    RULE signatureSrcIns : signature~;source |- src
    VIOLATION (TXT "{EX} InsPair_;src_;Signature_;", SRC I, TXT "_;Concept_;", TGT I)
    ROLE ExecEngine MAINTAINS signatureSrcDel
    RULE signatureSrcDel : src |- signature~;source
    VIOLATION (TXT "{EX} DelPair_;src_;Signature_;", SRC I, TXT "_;Concept_;", TGT I)

    ROLE ExecEngine MAINTAINS signatureTgtIns
    RULE signatureTgtIns : signature~;target |- tgt
    VIOLATION (TXT "{EX} InsPair_;tgt_;Signature_;", SRC I, TXT "_;Concept_;", TGT I)
    ROLE ExecEngine MAINTAINS signatureTgtDel
    RULE signatureTgtDel : tgt |- signature~;target
    VIOLATION (TXT "{EX} DelPair_;tgt_;Signature_;", SRC I, TXT "_;Concept_;", TGT I)

    ROLE ExecEngine MAINTAINS "id is surjective"
    RULE "id is surjective" : I[Concept] |- id~;id
    VIOLATION (TXT "{EX} InsPair_;id_;Term_;I[", SRC I, TXT "]_;Concept_;", SRC I)
ENDPROCESS

PROCESS "Defining type-terms"
    RELATION dom[Term*TypeTerm] [UNI]    -- TOT is maintained in RULE "dom is total"
    ROLE ExecEngine MAINTAINS "dom is total"
    RULE "dom is total" : I[Term]/\repr;repr~ |- dom;dom~
    VIOLATION (TXT "{EX} InsPair_;dom_;Term_;", SRC I, TXT "_;TypeTerm_;dom(", SRC repr, TXT ")")

    RELATION cod[Term*TypeTerm] [UNI]    -- TOT is maintained in RULE "cod is total"
    ROLE ExecEngine MAINTAINS "cod is total"
    RULE "cod is total" : I[Term]/\repr;repr~ |- cod;cod~
    VIOLATION (TXT "{EX} InsPair_;cod_;Term_;", SRC I, TXT "_;TypeTerm_;cod(", SRC repr, TXT ")")

    RELATION popset[Concept*TypeTerm] [UNI] -- TOT is maintained in RULE "popset is total"
    ROLE ExecEngine MAINTAINS "popset is total"
    RULE "popset is total" : I[Concept] |- popset;popset~
    VIOLATION (TXT "{EX} InsPair_;popset_;Concept_;", SRC I, TXT "_;TypeTerm_;pop(", SRC name, TXT ")")
ENDPROCESS

PROCESS "Analysing terms"
    RELATION pretype[TypeTerm*Concept]
    RELATION sub[TypeTerm*TypeTerm]
    RELATION typehalf[Term*Signature]

    ROLE ExecEngine MAINTAINS "Insert into pretype"
    RULE "Insert into pretype" : subStar;popset~ |- pretype
    VIOLATION (TXT "{EX} InsPair_;pretype_;TypeTerm_;", SRC I, TXT "_;Concept_;", TGT I)

    RELATION sub[TypeTerm*TypeTerm]
    MEANING "s sub g  means that each element of concept  s  is defined to be an element of concept  g  as well."
    RELATION subStar[TypeTerm*TypeTerm] -- Transitive, reflexive closure of sub, aka sub*
    MEANING "s subStar g  means that each element of type-term  s  is defined or inferred to be an element of type-term  g."
    RELATION subPlus[TypeTerm*TypeTerm] -- Transitive closure of sub, aka sub+

    RELATION subCopy[TypeTerm*TypeTerm] -- necessary only for calling the Transitive closure function.
    ROLE ExecEngine MAINTAINS "Compute transitive closure of sub"
    RULE "Compute transitive closure of sub" : sub = subCopy
    VIOLATION (TXT "{EX} TransitiveClosure;sub;TypeTerm;subCopy;subPlus")

    ROLE ExecEngine MAINTAINS "Compute transitive closure of sub by Ins", "Compute transitive closure of sub by Del"
    RULE "Compute transitive closure of sub by Ins" : subPlus\/I |- subStar
    VIOLATION (TXT "{EX} InsPair_;subStar_;TypeTerm_;", SRC I, TXT "_;TypeTerm_;", TGT I)
    RULE "Compute transitive closure of sub by Del" : subStar |- subPlus\/I
    VIOLATION (TXT "{EX} DelPair_;subStar_;TypeTerm_;", SRC I, TXT "_;TypeTerm_;", TGT I)

    ROLE ExecEngine MAINTAINS "INS isa INTO sub"
    RULE "INS isa INTO sub" : popset~;isa;popset |- sub
    VIOLATION (TXT "{EX} InsPair_;sub_;TypeTerm_;", SRC I, TXT "_;TypeTerm_;", TGT I)

    ROLE User MAINTAINS "Unambiguous pretype"
    RULE "Unambiguous pretype" : pretype~;pretype |- I[Concept]
    MEANING "For every type-term, there is at most one concept that is the pretype of that type-term."
    ROLE User MAINTAINS "Complete pretype"
    RULE "Complete pretype" : I[TypeTerm] |- pretype;pretype~
    MEANING "Every type-term must have a concept as its pretype."
ENDPROCESS

PROCESS Signatures
    RELATION src[Signature*Concept] [UNI]
    RELATION tgt[Signature*Concept] [UNI]
    RELATION signature[Relation*Signature]
    RELATION isaStar[Signature]
    ROLE ExecEngine MAINTAINS signatureIsasIns, signatureIsasDel
    RULE signatureIsasIns : src;isaStar;src~ /\ tgt;isaStar;tgt~ |- isaStar[Signature]
    VIOLATION (TXT "{EX} InsPair;isaStar;Signature;", SRC I, TXT ";Signature;", TGT I)
    RULE signatureIsasDel : isaStar[Signature] |- src;isaStar;src~ /\ tgt;isaStar;tgt~
    VIOLATION (TXT "{EX} DelPair;isaStar;Signature;", SRC I, TXT ";Signature;", TGT I)
ENDPROCESS

ENDCONTEXT