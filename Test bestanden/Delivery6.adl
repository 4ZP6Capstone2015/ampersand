CONTEXT Deliveries

OBJECT Bestelling : I[Order]
 = [ van : from
   , aan : issuedTo
   , betaald[Order] : from;paid;delivery;of /\ I
   ]

OBJECT Levering : I[Delivery]
 = [ leverancier : provider
   , klant : deliveredto
   , betaald : deliveredto;paid;delivery /\ I
   ]


OBJECT Bestellijst : I[ONE]
 = [ LijstItem : V[ONE*Delivery]
      = [ leverancier : provider
        , klant : deliveredto
        , betaald : deliveredto;paid;delivery /\ I
   ]    ]

PATTERN Deliveries
I[ONE] = V[ONE*ONE]
{-
 item :: Order * Product
  = [ ("C45666"                  , "Jelly beans"  )
    ; ("Order 22/09/2006 Cookies", "Cookies"      )
    ; ("C45683"                  , "Peanut butter")
    ].
 item :: Delivery * Product
  = [ ("Jelly beans #4921"  , "Jelly beans"  )
    ; ("Cookies #0382"      , "Cookies"      )
    ; ("Peanut butter #1993", "Peanut butter")
    ].
 item[Delivery*Product] |- of;item COMPUTING item[Delivery*Product]
  EXPLANATION "There must be an order for every item delivered."
 of;item |- item[Delivery*Product] COMPUTING item[Delivery*Product]
  EXPLANATION "All items ordered must be delivered."
-}
 issuedTo :: Order -> Provider PRAGMA "Order " " was issued to provider "
  = [ ("Order 22/09/2006 Cookies", "Candy's candy")
    ; ("C45666"                  , "Carter"       )
    ; ("C45683"                  , "Carter"       )
    ].
 of :: Delivery -> Order [SUR]
  = [ ("Cookies #0382"      , "Order 22/09/2006 Cookies")
    ; ("Jelly beans #4921"  , "C45666"                  )
    ; ("Peanut butter #1993", "C45683"                  )
    ].
 provider :: Delivery -> Provider PRAGMA "Provider " " has delivered delivery "
  = [ ("Cookies #0382"      , "Candy's candy")
    ; ("Jelly beans #4921"  , "Carter"       )
    ; ("Peanut butter #1993", "Carter"       )
    ].
 accepted :: Provider * Order [INJ,SUR] PRAGMA "Provider " " has accepted order "
  = [ ("Candy's candy", "Order 22/09/2006 Cookies")
    ; ("Carter"       , "C45666"                  )
    ; ("Carter"       , "C45683"                  )
    ].
 accepted |- issuedTo~ 
 EXPLANATION "not allowed to accept this order"

 SIGNAL order ON issuedTo~ |- accepted
 EXPLANATION "orders received."

 SIGNAL deliver ON accepted |- provider~;of
  EXPLANATION "deliverable orders"
 of;accepted~ |- provider COMPUTING provider
 provider |- of;accepted~
  EXPLANATION "No delivery is made to a client without an order accepted by the producer. (1.38)"

 deliveredto :: Delivery -> Client
  = [ ("Jelly beans #4921",	"Applegate")
    ; ("Cookies #0382",	"Brown")
    ; ("Peanut butter #1993",	"Conway")
    ].
 from :: Order -> Client
  = [ ("C45666"                  , "Applegate")
    ; ("Order 22/09/2006 Cookies", "Brown"    )
    ; ("C45683"                  , "Conway"   )
    ].
 of;from |- deliveredto COMPUTING deliveredto
  EXPLANATION "Deliveries are made to the client who ordered the delivery."

 sent :: Invoice -> Client PRAGMA "Invoice " " was sent to "
  = [ ("5362a", "Applegate")
    ; ("721i" , "Brown"    )
    ; ("9443a", "Conway"   )
    ].
 delivery :: Invoice -> Delivery [SUR] PRAGMA "Invoice " " covers delivery "
  = [ ("5362a", "Jelly beans #4921")
    ; ("721i" , "Cookies #0382"    )
    ; ("9443a", "Peanut butter #1993"   )
    ].
 delivery~;sent |- deliveredto
  EXPLANATION "A client accepts invoices for delivered orders only. (1.39)"
 sentBy :: Invoice -> Provider PRAGMA "Invoice " " has been sent by "
  = [ ("721i" , "Candy's candy")
    ; ("5362a", "Carter"       )
    ; ("9443a", "Carter"       )
    ].
 sentBy = delivery;of;issuedTo COMPUTING sentBy
  EXPLANATION "An invoice must sent by the provider to whom an order was issued."
SIGNAL pending ON provider = delivery~;sentBy
 EXPLANATION "invoices to be sent (1.40)"

 paid :: Client * Invoice [SUR,INJ] PRAGMA "Client " " has paid invoice "
  = [ ("Applegate", "5362a")
    ; ("Brown", "721i")
    ; ("Conway", "9443a")
    ].
 paid~ |- delivery;of;from EXPLANATION "Invoices must be paid by the client who ordered the delivery"
 SIGNAL payable ON delivery;of;from |- paid~
 EXPLANATION "Accept payments only for invoices sent."
 sent = delivery;of;from COMPUTING sent
ENDPATTERN
ENDCONTEXT