CONTEXT Meterkast
{- Dit is de ŽŽnversie van de Meterkast.
zo geven we straks PHPPLUG's aan:
  PHPPLUG GetPersonByNameAndAge {"ACTION=SELECT"} : I[Persoon]
ÊÊ= [ name {"PHPARG","PHPTYPE=String"} : naam
ÊÊÊÊ, age {"PHPARG","PHPTYPE=Int"} : leeftijd
ÊÊÊÊ, persoon {"PHPRETURN", "PHPTYPE=Array"} : I[Persoon]
ÊÊÊÊÊÊÊÊ= [ name : naam
ÊÊÊÊÊÊÊÊÊÊ, age Ê: leeftijd
ÊÊÊÊÊÊÊÊÊÊ, dateOfBirth : geboortedatum]
ÊÊÊÊ]
-}

SQLPLUG UserTbl : I[User]
 = [Id{"SQLTYPE=Id"} : I
   ,name{"SQLTYPE=Text"} : name
   ,password{"SQLTYPE=Password"} : password
   ,teacher{"SQLTYPE=Id"} : teacher
   ]
SQLPLUG RightTbl : I[Operation]
 = [Id{"SQLTYPE=Id"} : I
   ,name{"SQLTYPE=Text"} : name
   ]
SQLPLUG ActieTbl : I[Actie]
 = [Id{"SQLTYPE=Id"} : I
   ,object{"SQLTYPE=Id"} : object
   ,type{"SQLTYPE=Id"} : right
   ]
SQLPLUG BestandTbl : I[Bestand]
 = [Id{"SQLTYPE=Id"} : I
   ,owner{"SQLTYPE=Id"} : owner
   ,path{"SQLTYPE=Text"} : path
   ]

PATTERN Meterkast

name      :: User->Text [INJ].
name      :: Operation->Text.
path      :: Bestand->Text.
password  :: User->Password.
owner     :: Bestand -> User PRAGMA "the owner of " " is ".
teacher   :: User * User [UNI] PRAGMA "the teacher of " " is ".
right     :: User * Operation PRAGMA "" " may exercise ".
object    :: Actie -> Bestand PRAGMA "" " is being executed on ".
-- user      :: Actie -> User PRAGMA "" " is being executed by user ".
-- user=object;owner
right     :: Actie -> Operation PRAGMA "" " is being executed by activating ".

ENDPATTERN


  SERVICE Users : I[ONE]
   = [ Users : V[ONE*User]
        = [ nr      : I[User]
          , name    : name
          , pwd     : password
          , rechten : right
          , files   : owner~
          ]
     ]

  SERVICE User : I[User]
   = [ naam    : name
     , pwd     : password
     ]

  SERVICE Userbeheer : I[User]
   = [ credentials : I = [ naam      : name
                         , pwd       : password
                         , teacher   : teacher
                         ]
     , bestanden : owner~
     , studenten : teacher~
     , rechten   : right
     ]


  SERVICE Student : teacher;teacher~ /\ I
   = [ naam    : name
     , docentNaam  : teacher;name
     , files   : owner~
     ]

-- De volgende definitie wordt incorrect vertaald:
  SERVICE Docenten : I[ONE]
   = [ Docenten : V[ONE*User];(teacher~;teacher /\ I)
        = [ nr               : I[User]
          , name             : name
          ]
     ]

-- SERVICE Docent SERVICES Docent EDITS I[User], I[Right]
  SERVICE Docent : teacher~;teacher /\ I
   = [ naam      : name
     , bestanden : owner~
     , studenten : teacher~
        = [ naam      : name
          , bestanden : owner~
          ]
     ]
  
  SERVICE Bestand : I[Bestand]
   = [ eigenaar : owner
     , acties   : object[Actie*Bestand]~
        = [ nr   : I[Actie]
          , type : right
          ]
     ]

  SERVICE Actie : I[Actie]
   = [ file     : object
     , operatie : right
     ]


ENDCONTEXT
